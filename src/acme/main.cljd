(ns acme.main
  (:require ["package:flutter/material.dart" :as m]
            ["package:flutter_tts/flutter_tts.dart" :refer [FlutterTts]]
            [cljd.flutter :as f]))

(defn main []
  (f/run
   :watch [enabled? (atom true) :as button-state
           number (atom 0) :as counter]
   :managed [tts (doto (FlutterTts)
                   (.setLanguage "en-US")
                   (.setCompletionHandler #(reset! button-state true)))
             :dispose .stop]
   (m/MaterialApp
    .title "Welcome to Flutter"
    .theme (m/ThemeData .primarySwatch m.Colors/pink))
   .home
   (m/Scaffold
    .appBar (m/AppBar
             .title (m/Text "Welcome to ClojureDart")))
   .body
   m/Center
   (m/OutlinedButton
    .onPressed
    (when enabled?
      (fn press-callback []
        (reset! button-state false)
        (let [new-handler (fn temporary-completion-handler []
                            ;; reset the handler to the original one first
                            (.setCompletionHandler tts #(reset! button-state true))
                            ;; in this handler, do something else!
                            (reset! button-state true)
                            (swap! counter inc))]
          (.setCompletionHandler tts new-handler)
          (.speak tts "Let's get coding!")))))
   (m/Text
    (str "Get some cheers! x" number)
    .style (m/TextStyle
            .color m.Colors/red
            .fontSize 32.0))))
